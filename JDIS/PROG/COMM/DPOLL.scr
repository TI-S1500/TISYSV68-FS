##################################################################
#
#  DPOLL.scr
#
#      Version 1.0  (xenix platform)
#      Release 8.2
#          14 FEB 92 - Added wait 2 before file transfers per CRG
#  Script variables:
#   @RTNCODE   - the return code I will pass back to aspen
#      0 - the call ended with no errors
#      1 - operator aborted while waiting for a call
#      2 - operator aborted during the first error-free session
#      3 - operator aborted during the second error-free session
#      4 - error occurred during the first error-free session
#      5 - error occurred during the second error-free session
#      9 - did not receive HOSTID.DAT and/or HOST.EFF files
#   @ANSRC     - the return code from the modem script
#      0 - successfully connected with the host
#      1 - operator aborted while waiting for a connect
#
##################################################################
#
        display "******************************************************"
        display "DPOLL.scr starting"
#
        set @RTNCODE = "0"
#-----------------------------------------------------------------
#  Check to see if the Blast poll init script exists
#  If yes, execute it
#-----------------------------------------------------------------
        if EXIST "/JDIS/PROG/COMM/DPOLLINT.scr"
                CALL "/JDIS/PROG/COMM/DPOLLINT.scr"
        end
#-----------------------------------------------------------------
#  Display the screen header if in silent mode
#-----------------------------------------------------------------
        set @hdrtn = "H1"
        goto .HEADING
.H1
#-----------------------------------------------------------------
#  Get the dealer answerback from the TRPASSWD.DAT file
#-----------------------------------------------------------------
        fopenr 1,"TRPASSWD.DAT"
        fread  1,@REC
        fclose 1
        display "@REC: ",@REC
        set @TRPASSWD = @REC
        STRTRIM @TRPASSWD,11,18
        display "TRPASSWD:  >>>",@TRPASSWD,"<<<"
#-----------------------------------------------------------------
#  Display the Blast copyright stmt
#-----------------------------------------------------------------
#
        SET @SCRLREG = "1"
        CURSOR 10,15
        PUT "+--------------------------------------------------+"
        CURSOR 11,15
        PUT "|                                                  |"
        CURSOR 12,15
        PUT "|   BLAST                                          |"
        CURSOR 13,15
        PUT "|   (C) 1986 Communications Research Group         | "
        CURSOR 14,15
        PUT "|                                                  |"
        CURSOR 15,15
        PUT "|   (BLAST is registered in U.S. Pat & Tm. Off.)   |"
        CURSOR 16,15
        PUT "|                                                  |"
        CURSOR 17,15
        PUT "+--------------------------------------------------+"
        SET @SCRLREG = "0"
        WAIT 3
#
#-----------------------------------------------------------------
#  Do CONNECT to wait for the host to call us
#-----------------------------------------------------------------
.WTCONN
        set @hdrtn = "H2"
        goto .HEADING
.H2
        SET @SCRLREG = "1"
        CURSOR 10,1
        PUT "Waiting for a call from the host"
        CURSOR 22,1
        PUT "(Note: to interrupt, press control-K ONCE ONLY)"
        SET @SCRLREG = "0"
#               turn the keyboard on again
        set @KEYBOARD = 1
#
        CONNECT
#
#-----------------------------------------------------------------
#  Ck to see if the operator aborted the session
#   . @ANSRC = 0 connected ok
#   . @ANSRC = 1 means operator aborted the session
#-----------------------------------------------------------------
#
        display "connect return status: " @ANSRC
        if @ANSRC = "0" goto .GOTCONN
        display "Operator aborted the session.  rtn code:" @ANSRC
# 
        SET @SCRLREG = "1"
        CURSOR 10,1
        CLEAR
        PUT "Operation cancelled by the operator."
        SET @SCRLREG = "0"
        set @RTNCODE = "1"
        goto .EXIT
#-----------------------------------------------------------------
#  Do the first eff_free transfer to send & receive the id files
#-----------------------------------------------------------------
.GOTCONN
        set @hdrtn = "H3"
        goto .HEADING
.H3
        SET @SCRLREG = "1"
        CURSOR 10,1
#
        PUT "Doing the first err_free session"
        SET @SCRLREG = "0"
        display "First eff starting"
#
        wait 2 idle
        FILETRANSFER
        if @EFERROR NOT = "0" goto .EFF1ERR
        FILE
        TRPASSWD.DAT
        FILE
        EFF1.EFF
        ESC
        if @EFERROR = "0" goto .EFF1OK
#-----------------------------------------------------------------
#  An error occurred during the error-free session
#-----------------------------------------------------------------
.EFF1ERR
        display "FIRST ERROR-FREE SESSION ENDED WITH ERROR - code " @EFERROR
#  ck for operator cancel during the eff session
        if @EFERROR = "-2"
                display "Session cancelled by the operator"
                set @RTNCODE = "2"
                goto .DROP
        end
#  some other error occurred - go drop the line and leave
        set @RTNCODE = "4"
        goto .DROP
#-----------------------------------------------------------------
#  First error-free transfer completed without error
#-----------------------------------------------------------------
.EFF1OK
        display "First eff complete"
        SET @SCRLREG = "1"
        CURSOR 12,1
#
        PUT "The first err_free session is complete"
        SET @SCRLREG = "0"
#-----------------------------------------------------------------
#  Ck to make sure we got a HOSTID & HOSTEFF file
#-----------------------------------------------------------------
        if not exist HOSTID.DAT OR not exist HOST.EFF
                set @RTNCODE = "9"
                goto .DROP
        end
#
        display "The host id file was received ok"
        ltype "HOSTID.DAT"
#-----------------------------------------------------------------
#  Do the second err_free mode to send the actual data files
#-----------------------------------------------------------------
.EFF2
        set @hdrtn = "H4"
        goto .HEADING
.H4
        display "Starting error-free file transfer"
        SET @SCRLREG = "1"
        CURSOR 10,1
#
        PUT "Doing the second err_free session"
        SET @SCRLREG = "0"
#
        wait 2 idle
        FILETRANSFER
        if @EFERROR NOT = "0" goto .EFF2ERR
        FILE
        TRPASSWD.DAT
        FILE
        EFF2.EFF
        ESC
        if @EFERROR = "0" goto .EFF2OK
#-----------------------------------------------------------------
#  An error occurred during the error-free session
#-----------------------------------------------------------------
.EFF2ERR
   display "SECOND ERROR-FREE SESSION ENDED WITH ERROR - code " @EFERROR
#  ck for operator cancel during the eff session
        if @EFERROR = "-2"
                display "Session cancelled by the operator"
                set @RTNCODE = "3"
                goto .DROP
        end
#  some other error occurred - drop the line and leave
        set @RTNCODE = "5"
        goto .DROP
#-----------------------------------------------------------------
#  Second error-free transfer completed without error
#-----------------------------------------------------------------
.EFF2OK
        display "File transfer is complete"
        set @hdrtn = "H5"
        goto .HEADING
.H5
        SET @SCRLREG = "1"
        CURSOR 10,1
#
        PUT "File transfer is complete"
#
#-----------------------------------------------------------------
#  Drop the modem connection
#-----------------------------------------------------------------
.DROP
        CURSOR 12,1
        PUT "Disconnecting now"
        SET @SCRLREG = "0"
#
        ECHO ON
        DISCONNECT
        display "modem disconnect complete"
#-----------------------------------------------------------------
#  We exit blast here - RTNCODE must be set before coming here
#-----------------------------------------------------------------
.EXIT
# send back the return code
        fopena 1,"/JDIS/PROG/COMM/DPOLLSYN"
        fwrite 1,"S AS BLASTRC " @RTNCODE
        fwrite 1,"*****endfile******"
        fclose 1
#
        display "Exiting blast"
        display "Exit code = " @RTNCODE
#
        quit
#
#
####################################################################
###                      SUBROUTINES
####################################################################
#-----------------------------------------------------------------
#  display the screen heading 
#-----------------------------------------------------------------
.HEADING
        SET @SCRLREG = "1"
        CLEAR
        CURSOR 1,25
        PUT "JOHN DEERE INFORMATION SYSTEMS"
        CURSOR 3,24
        PUT "DPOLL Host Communications Utility"
        SET @SCRLREG = "0"
#
        if @hdrtn = "H1" goto .H1
        if @hdrtn = "H2" goto .H2
        if @hdrtn = "H3" goto .H3
        if @hdrtn = "H4" goto .H4
        if @hdrtn = "H5" goto .H5
        quit
#####
